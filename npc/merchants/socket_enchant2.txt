//===== rAthena Script ======================================= 
//= Episode 12 Socket Enchant NPC
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================ 

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos items. -";
		mes "- Por favor tente novamente -";
		mes "- depois que você diminuir o seu peso. -";
		close;
	}
	mes "[Leablem]";
	mes "Bom dia. Meu nome é Leablem,";
	mes "e eu sou um técnico que especializa em";
	mes "adicionar slots/encaixes à armas e armaduras.";
	mes "Pode ser muito difícil as vezes, mas eu gosto";
	mes "do que eu faço, tenho orgulho nisso.";
	next;
	mes "[Leablem]";
	mes "Eu aprendi a maioria das coisas com meu irmão, Seyablem,";
	mes "mas eu acho que ainda tenho muito para aprender.";
	mes "Então por favor, entenda que meu serviço de encaixe é limitado,";
	mes "ao invés do meu irmão Seyablem.";
	next;
	mes "[Leablem]";
	mes "Meu irmão não estava muito feliz em me ensinar";
	mes "suas habilidades porque ele estava preocupado que suas habilidades";
	mes "não seriam mais únicas. Mas mesmo assim ele tentou seu máximo";
	mes "em me ensinar.";
	next;
	mes "[Leablem]";
	mes "Graças a ele, Eu aprendi muitas coisas.";
	mes "Eu não sou muito confiante";
	mes "em tentar coisas diferentes,";
	mes "mas eu vou fazer o que";
	mes "eu posso por enquanto.";
	next;
	mes "[Leablem]";
	mes "Aliás, tem uma coisa";
	mes "que meu irmão não quer que eu faça.";
	mes "Eu não quero aborrecer ele, então...vamos pular essa parte.";
	mes "Eu não vou falar mais nada!";
	next;
	mes "[Leablem]";
	mes "De qualquer forma, você tem algum equipamento";
	mes "que você quer adicionar slots?";
	mes "Minha comissão, os materiais,";
	mes "e a chance de sucesso dependem do";
	mes "item específico que irei trabalhar.";
	next;
	switch(select("Arma:Armadura:Sobre aquilo que você pulou:Mais informações:Sair"))
	{
		case 1:
			mes "[Leablem]";
			mes "Arma, você disse?, Tudo bem, sem problemas.";
			mes "Armas são divididas em 4 classes";
			mes "dependendo da dificuldade do trabalho.";
			mes "A classe C é a mais fácil e a classe é a mais difícil.";
			mes "Que classe você quer tentar?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes "Classe C, tudo bem.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Book of Mother Earth:Book of Billows:Book of Gust of Wind:Book of the Blazing Sun"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes "Você escolheu a mediana classe B.";
					mes "Então, que tipo de item você tem?";
					next;
					switch(select("Orcish Axe:Scimiter:Spike"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "Classe A? Você parece querer um pouco demais, Mas, sem problemas.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Dragon Killer:Katar of Quaking:Katar of Raging Blaze:Katar of Frozen Icicle:Katar of Piercing Wind:Golden Mace:Oriental Lute:Queen's Whip:Spectral Spear:Gae Bolg:Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes "Huh, Classe S?! Meu Deus, é sério?*Treme*";
					mes "Então...que arma classe S você quer que eu faça um slot?!";
					next;
					switch(select("Zephyrus:Mailbreaker:Dragon Slayer:Swordbreaker:Assasin Dagger:Grand Cross:Executioner"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "Você disse armadura? Claro, sem problemas.";
			mes "Armaduras são divididas em 4 classes diferentes";
			mes "dependendo da dificuldade do trabalho.";
			mes "A classe C é a mais fácil e a classe S é a mais difícil.";
			mes "Que classe de armadura você quer tentar?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes "Classe C, entendo.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Sunflower:Ph.D Hat:Big Ribbon:Boys Cap"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes "Você escolheu a classe B.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Skull Ring:High Heels"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "Classe A? Parece que você quer um pouco demais, Mas, tudo bem.o much.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Pirate Bandana:Black Leather Boots"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[Leablem]";
					mes "Huh, Classe S?! Meu Deus, você está brincando?*Treme*";
					mes "Então...que armadura classe S você quer que eu faça um slot?!";
					next;
					switch(select("Mage Coat:Holy Robe:Sacred Mission:Undershirt:Pantie"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "Ah, não é nada especial, mas ele acha";
			mes "que não é algo que os humanos devem ficar mexendo. ";
			mes "Eu não quero deixar ele bravo,";
			mes "então é melhor eu esquecer isso.";
			next;
			mes "[Leablem]";
			mes "...Aquele serviço em si tem uma taxa de sucesso muito alta,";
			mes "mas é necessário um monte de materiais e uma comissão alta. ";
			mes "Eu duvido que alguém iria querer usar";
			mes "o serviço, mesmo que eu disponibilizasse";
			next;
			mes "[Leablem]";
			mes "...Ummm...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "Você parece interessado em usar esse serviço.";
			mes "Se você quiser, eu posso pelo menos te dizer sobre ele. ";
			mes "Mas você precisa manter isso um segredo.";
			mes "Se meu irmão descobrir sobre o que eu vou te dizer,";
			mes "ele vai me bater!";
			next;
			mes "[Leablem]";
			mes "Esse serviço especial tem uma chance de sucesso de 90%,";
			mes "mas é necessário 2 Ouros e 200 milhões de zeny:";
			mes "Eu posso adicionar slots ao Chapéu do Deus do Sol.";
			next;
			mes "[Leablem]";
			mes "Meu irmão disse que, desde tempos antigos, é proibido modificar esse chapéu. ";
			mes "Mas eu acredito que os humanos são muito curiosos para não tentar";
			mes "experimentar, especialmente quando é proibido.";
			mes "Eu apenas não quero que meu irmão grite comigo. Isso é tudo.";
			next;
			switch(select("Usar o serviço","Sair"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...O qu... O quê? Não!";
					mes "Me olhando torto";
					mes "não vai fazer com que eu faça!";
					mes "Não, Eu disse não! Não é não! Não!";
					next;
					mes "[Leablem]";
					mes "Não, Eu não vou! Não!";
					next;
					mes "[Leablem]";
					mes "Eu não posso fazer isso...";
					next;
					mes "[Leablem]";
					mes "Eu não posso fazer... Isso...";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "Umm...";
					next;
					mes "[Leablem]";
					mes "Você é tão persistente. Tudo bem, Eu vou fazer para você uma exceção.";
					mes "Apenas não conte para niguém,";
					mes "especialmente meu irmão. Você promete??";
					next;
					mes "[Leablem]";
					mes "Tudo bem então, Por favor me traga";
					mes "^FF00002 Ouros, 200 milhões de zeny -- a comissão --,";
					mes "^FF0000e um chapéu do Deus do Sol.";
					mes "^FF0000Você sabe que a chance de sucesso é 90% com isso,não sabe?^000000";
					mes "Nós não temos tempo para perder,";
					mes "então vamos à trabalho logo.";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "Devemos começar??";
						mes "Reze para seus Deuses por boa sorte.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "Excelente! Nossa, eu acho que os 90% de chance de sucesso é verdade!!";
							mes "Olha, o slot foi adicionado no local perfeito.";
							mes "Parabéns.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes "Essa é a primeira e última vez que eu vou fazer isso para você.";
							mes "Nunca conte para ninguém";
							mes "sobre isso, tudo bem?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "Ah! Meu deus, nós falhamos!";
							mes "Meu deus, Eu acho que nós tivemos o pior tipo de sorte...";
							mes "O que devemos fazer? ...Deve ser por conta de que";
							mes "Eu não queria fazer isso desde o começo!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "Não, nunca mais me pergunta para fazer algo tão arriscado novamente!";
							mes "Bah~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "Umm...";
						next;
						mes "[Leablem]";
						mes "Você não tem materiais suficientes.";
						mes "Cara, por que voce se quer me perguntou para tentar?";
						mes "Eu não vou fazer isso de novo para você,";
						mes "mesmo se você trouxer materiais suficientes. Bah~";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "Nunca conte isso para ninguém";
					mes "sobre o que eu te disse. Tudo bem?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "Você quer saber mais informações?";
			mes "Bom, eu não acho que tem algo";
			mes "que você precisa saber especificamente.";
			mes "Mas eu vou te dar pelo menos uma dica.";
			next;
			mes "[Leablem]";
			mes "Os técnicos de slot em cada cidade";
			mes "possuem diferentes chances de sucessos de criar slots";
			mes "dependendo do nível do seu equipamento.";
			mes "Eu não sei se é verdade,";
			mes "mas eu consigo ver a diferença em cada cidade.";
			next;
			mes "[Leablem]";
			mes "Por exemplo, o técnido de slot em cada cidade";
			mes "possui uma taxa de sucesso maior para";
			mes "equipamentos com níveis maiores,";
			mes "enqunto técnicos em outras cidade tem chance maior de";
			mes "sucesso nos niveis mais baixos de equipamento.";
			next;
			mes "[Leablem]";
			mes "Quando você falhar em adicionar um slot, o equipamento";
			mes "vai se quebrar e desaparecer.";
			mes "Eu vou tentar meu melhor para dar certo, mas";
			mes "adicionar slots é complicado.";
			next;
			mes "[Leablem]";
			mes "Ah, tem uma coisa importante de que você precisa ser cuidadoso.";
			mes "^FF0000Tente não carregar dois items idênticos";
			mes "^FF0000com você quando voce adicionar slots.";
			mes "^FF0000Se não, slots podem ser adicionados à um item escolhido aleatóriamente,";
			mes "^FF0000e no pior caso, você pode perder o item que tem mais valor para você.^000000";
			next;
			mes "[Leablem]";
			mes "Espero que essa informação seja útil para você.";
			mes "Obrigado.";
			close;
		case 5:
			mes "[Leablem]";
			mes "Tudo bem, então.";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Leablem]";
	if (.@item_req[1] != 0 && .@req_amount[1] != 0)
	{
		mes getitemname(.@item_id) +"... Tudo bem, você precisa me trazer";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny como comissão,";
		mes "^FF0000"+ .@req_amount[0] +"ea "+ getitemname(.@item_req[0]) +" e "+ .@req_amount[1] +"ea "+ getitemname(.@item_req[1]) +".";
		mes "^FF0000Claro, você precisa de "+ getitemname(.@item_id) +".^000000";
	}
	else
	{
		mes getitemname(.@item_id) +"? Beleza, então precisa me trazer";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) +",000 zeny como comissão e "+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +" como requisito.";
		mes "^FF0000Claro, não esqueça de me trazer "+ getitemname(.@item_id) +".";
		mes "^FF0000Você deve ter todos os itens.^000000";
	}
	next;
	mes "[Leablem]";
	mes "Você trouxe todos eles??";
	mes "Para sua informação, se você falhar em criar um slot,";
	mes "você vai perder todos os itens requisitados, assim como o alvo "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "weapon" : "armor") +".";
	mes "Também lembre, se o "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "weapon" : "armor") +" foi refinado, e também tiver uma carta inserida,";
	mes "você vai perde-los mesmo que tenha sucesso em criar um slot.";
	next;
	switch(select("Perguntar por criação de Slot.:Tentar uma próxima vez."))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no .@req_amount[0] and .@req_amount[1] provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Leablem]";
				mes "Tudo bem então, vamos começar o trabalho!";
				mes "É melhor você rezar para um bom resultado.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes "Ótimo, parece que foi um sucesso.";
					mes "Parece que foi bem feito. Parabéns!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes "Te vejo de novo, camarada!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "O quê! ...Me desculpe, mas falhou.";
					mes "De qualquer modo, Eu sou completamente inocente.";
					mes "Essa é a sua sorte, e também é destinado por deus, tudo bem?";
					mes "Não fique desapontado,";
					mes "e tente na próxima vez.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Leablem]";
					mes "Te vejo novamente, camarada!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes "Você é estúpido ou o quê? Você não trouxe todo os itens requisitados!";
				mes "Traga eles rápido!";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "Te vejo novamente.";
			close;
	}
}
