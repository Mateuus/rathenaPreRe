//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está¡ carregando -";
		mes "- item demais com você. -";
		mes "- Por favor tente novamente -";
		mes "- depois de perder um pouco de peso. -";
		close;
	}
	mes "[Seiyablem]";
	mes "Eu sou um engenheiro que especializa em adicionar slots em Armas e Armaduras normais.";
	mes "Minha comissão, os materiais requeridos e a chance de sucesso dependem do item específico que irei trabalhar.";
	next;
	mes "[Seiyablem]";
	mes "Adicionar Slots pode parecer simples, mas é muito mais complicado do que parece.";
	mes "Se estiver interessado no meu serviço, me avise.";
	next;
	switch(select("Adicionar Slot em Arma:Adicionar Slot em Armadura:Informação de tentativa de Slot:Cancelar"))
	{
		case 1:
			mes "[Seiyablem]";
			mes "Você quer adicionar um Slot em uma arma?";
			mes "Eu categorizo as armas pelo dificuldade em adicionar um Slot, a Classe C é a mais fácil e a Classe S é a mais difícil.";
			mes "Quais das classes você quer tentar?";
			next;
			switch(select("Classe C:Classe B:Classe A:Classe S"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Classe C, em?";
					mes "Agora, Eu posso apenas usar certos equipamentos nessa classe, então por favor escolha um dos seguintes itens, para nós tentarmos adicionar um Slot nele.";
					next;
					switch(select("Trident:Rope:Violin"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes "Classe B?";
					mes "Beleza, essa é a categoria de equipamento mediana.";
					mes "Por favor escolha a arma que voê quer que eu tente adicionar um Slot.";
					next;
					switch(select("Chain:Gladius:Gakkung Bow:Pike:Haedonggum:Lute:Wire:Waghnakh:Arbalest Bow"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Ooh, Classe A.";
					mes "Esse é um território arriscado!";
					mes "Beleza, que arma você quer que eu tente adicionar um Slot?";
					next;
					switch(select("Hunter Bow:Survivor's Rod(INT):Zweihander:Flamberge:Infiltrator:Ballista:Stunner:Berserk:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes "Whoa, whoa, whoa...";
					mes "Classe S? Tudo bem...";
					mes "Vai ser quase um milagre se eu conseguir fazer isso.";
					mes "Beleza... Que arma devemos tentar adicionar um Slot?";
					next;
					switch(select("Gungnir:Poison Knife:Ice Pick:Sucsamad:Ginnungagap:Cutlas:Crescent Scythe:Survivor's Rod(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes "Armadura, você disse?";
			mes "Tudo bem, sem problemas.";
			mes "Armadura é dividida em 4 diferentes classes, dependendo assim da dificuldade do trabalho.";
			mes "Classe C é a mais fácil e a Classe S é a maid difícil.";
			mes "Qual classe você gostaria de tentar?";
			next;
			switch(select("Classe C:Classe B:Classe A:Classe S"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Armadura Classe C, em?";
					mes "Parece rasoável.";
					mes "Qual armadura você gostaria de tentar adicionar um Slot?";
					next;
					switch(select("Mantle:Coat:Circlet:Biretta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "Você escolheu a razoável Classe B.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Mirror Shield:Chain Mail:Saint Robe:Silk Robe:Boots:Shoes:Muffler:Guard:Buckler:Shield:Bongun Hat"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Ooh, Classe A.";
					mes "Esse é um território perigoso!";
					mes "Tudo bem, que armadura você quer que eu tente adicionar um Slot?";
					next;
					switch(select("Gemmed Sallet:Bucket Hat:Memory Book:Tights:Legion Plate Armor:Full Plate:Thief Clothes:Greaves:Coif:Manteau:Helm:Ninja Suit:Orc Helm:Ancient Cape:Monk Hat:Golden Gear:Brooch:Munak Hat"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "Um... Classe S?";
					mes "Você tem certeza?";
					mes "Beleza... Me diga a Armadura que você quer que eu adicione um Slot, e eu vou lhe dizer o que eu preciso para trabalhar.";
					next;
					switch(select("Majestic Goat:Spiky Band:Bone Helm:Corsair:Crown:Tiara:Sphinx Hat:Robe of Cast:Earring:Ring:Bow Thimble"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "Bom, eu realmente não refinei a arte da Adição de Slot.";
			mes "É tão complicado que eu estaria mentindo se te contasse que eu sabia cada fator que afeta o processo.";
			mes "Ainda assim, noto algumas tendências...";
			next;
			mes "[Seiyablem]";
			mes "Em algumas cidades, as tentativas de adição de slots são mais bem-sucedidas para equipamentos com menos atualizações/upgrades";
			mes "Em outras cidades, acontece o contrário.";
			mes "não é realmente peculiar?";
			next;
			mes "[Seiyablem]";
			mes "Ah, tem uma coisa muito importante que você deveria saber.";
			mes "Se você me pedir para adicionar um Slot em alguma coisa, tenha certeza de que você não tem diversos desse mesmo item no seu inventário.";
			next;
			mes "[Seiyablem]";
			mes "Um exemplo: se você tiver um Manteau +7 e um Manteau normal no seu inventário, eu vou pegar aleatóriamente um deles para adicionar um Slot.";
			mes "Apenas se lembre de ser cuidadoso.";
			next;
			mes "[Seiyablem]";
			mes "Repetindo, ^FF0000apenas carregue um equipamento que você quer que eu adicione um Slot.^000000.";
			mes "Todos os outros equipamentos com o mesmo nome devem ter colocados no seu armazém da Kafra, entendeu?";
			close;
		case 4:
			mes "[Seiyablem]";
			mes "Calma, aventureiro.";
			mes "Se você quer tentar adicionar um Slot em algum equipamento, volte e me avise.";
			mes "Até mais~";
			close;
	}
}

function	script	Func_Socket	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Seiyablem]";
	mes "Você quer adicionar um Slot em um "+ getitemname(.@item_id) +"?";
	mes "Beleza, por favor me traga ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") : "") + "and my ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny^000000 service fee.";
	mes "Ah, não se esqueça de me trazer o "+ getitemname(.@item_id) +"!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(.@item_id,5) & EQP_HAND_R) // weapon
	{
		mes "Eu posso tentar adicionar um slot agora se voce tem os itens e o zeny que eu pedi.";
		mes "De qualquer forma, você deve saber que tem uma chance que eu possa falhar.";
		mes "Então, eu preciso te avisar...";
		next;
		mes "[Seiyablem]";
		mes "Se a tentativa de adicionar um Slot em sua Arma falhar, então a ^FF0000Arma^000000, ^FF0000 e qualquer Carta inserida será destruída^000000.";
	}
	else // armor
	{
		mes "Se você tiver todos os materiais requisitados, minha comissão e a Armadura, então nós podemos prosseguir com a tentativa de adicionar um Slot.";
		mes "Mas antes disso, Eu preciso te avisar sobre o risco.";
		next;
		mes "[Seiyablem]";
		mes "Se a tentativa de adicionar um Slot em sua Armadura falhar, então a ^FF0000Armadura^000000, ^FF0000os upgrades/as atualizações^000000 ^FF0000e qualquer Carta inseridas serão destruídas^000000.";
	}
	mes "Você ainda quer tentar adicionar um Slot?";
	next;
	switch(select("Tentar adição de Slot:Cancelar"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "Bom, mãos à obra!";
				mes "É melhor você rezar por um resultado positivo.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "Ótimo, parece que foi um sucesso.";
					mes "Parece que foi bem feito. Parabéns!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "Até mais!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "Quê! ...Me desculpe, o processo falhou.";
					mes "De qualquer forma, Eu sou completamente inocente.";
					mes "Essa é a sorte, e é destinado por Deus, okay?";
					mes "Não fique tão zangado, tente uma próxima vez.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Seiyablem]";
					mes "Eu te desejo boa sorte na próxima vez!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "Eu gostaria de fazer a Adição de Slot, mas você não tem um dos requisitos.";
				mes "Você tem certeza que tem o equipamento, os materiais e os zenys?";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "Precisa de um tempo para pensar, em?";
			mes "Beleza, Eu entendo.";
			mes "Apenas lembre que a vida não é divertida se você está sempre em sua área de segurança~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes "Eu estou considerando me tornar um Engenheiro de Slots.";
	mes "É um novo campo em que você adiciona Slots em equipamentos, seria legal se eu pudesse trabalhar em um ramo tão lucrativo.";
	next;
	mes "[Young Man]";
	mes "A sorte tem uma grande participação no processo de adicionar Slots com sucesso pelo que eu sei.";
	mes "As vezes você falha, mas se você tiver muita sorte, você pode adicionar dois Slots.";
	mes "Loucura. não é?";
	next;
	mes "[Young Man]";
	mes "Mas não fique muito excitado.";
	mes "Apenas alguns equipamentos podem aguentar a adição de dois Slots extras.";
	mes "Tem muito ainda que eu tenho que aprender antes de me tornar apenas um aprendiz...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	97
payon,143,143,7	duplicate(SocketMan)	Young Man#pay	97
morocc,60,42,3	duplicate(SocketMan)	Young Man#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	97
